--  Based on scipy/scipy/optimize/lbfgsb_src/lbfgsb.f

with Interfaces.Fortran; use Interfaces.Fortran;

with NL_Arrays_And_Matrices; use NL_Arrays_And_Matrices;

package Lbfgsb_F_Interface is

   type Fortran_DP_Array is array (Integer range <>) of Double_Precision;
   pragma Convention (Fortran, Fortran_DP_Array);

   type Fortran_Integer_Array is array (Integer range <>) of Integer;
   pragma Convention (Fortran, Fortran_Integer_Array);

   type Fortran_LSave_Array is array (Integer range 1 .. 4) of Integer;
   pragma Convention (Fortran, Fortran_LSave_Array);

   type Fortran_ISave_Array is array (1 .. 44) of Integer;
   pragma Convention (Fortran, Fortran_ISave_Array);

   type Fortran_DSave_Array is array (Integer range 1 .. 29) of
     Double_Precision;
   pragma Convention (Fortran, Fortran_DSave_Array);

   type Character_60 is new Fortran_Character (1 .. 60);

    --  x is a double precision array of dimension n.
    --  On entry x is an approximation to the solution.
    --  On exit x is the current approximation.
    --  nbd is an integer array of dimension n.
    --  On entry nbd represents the type of bounds imposed on the
    --  variables and must be specified by:
    --  nbd(i)=0 if x(i) is unbounded,
    --         1 if x(i) has only a lower bound,
    --         2 if x(i) has both lower and upper bounds, and
    --         3 if x(i) has only an upper bound.
    --  factr is a double precision variable specified on entry as factr >= 0
    --  The iteration stops when
    --     (f^k - f^{k+1})/max{|f^k|,|f^{k+1}|,1} <= factr*epsmch
    --     where epsmch is the machine precision, automatically
    --     generated by the code.
    --  Typical values for factr: 10 ** 12 for low accuracy;
    --  10 ** 7 for moderate accuracy and 10 for extremely high accuracy.
    --  wa is a double precision working array of length
    --  (2mmax + 5)nmax + 12mmax^2 + 12mmax.
    --  iwa is an integer working array of length 3nmax.
   procedure setulb (m    : in Fortran_Integer;
                     x    : in out Fortran_DP_Array;
                     l, u : in Fortran_DP_Array;  --  bounds
                     nbd  : in Fortran_Integer_Array;
                     f    : in out Double_Precision;
                     g            : in out Fortran_DP_Array;
                     factr, pgtol : in out Double_Precision;
                     wa           : in out Fortran_DP_Array;
                     iwa          : in out Fortran_Integer_Array;
                     TaskName     : in out Character_60;
                     iprint       : in Fortran_Integer;
                     csave : in out Character_60;
                     lsave : in out Fortran_LSave_Array;
                     isave : in out Fortran_Integer_Array;
                     dsave : in out Fortran_DSave_Array;
                     maxls : in Fortran_Integer);
   pragma Import (Fortran, setulb);

   function To_DP_Array (RA : Real_Float_Vector) return Fortran_DP_Array;
   function Zero_Array (Num_Rows : Positive) return Fortran_DP_Array;

end Lbfgsb_F_Interface;
